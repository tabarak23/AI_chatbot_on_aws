

#apis 
# in this file ill be defineing rest api gateway, resources, methods, integrations, CORS, deployment, and permissions

# locals

locals {
  api_name = "${var.project_name}-${var.stage}-api"

  common_tags = {
    Project= var.project_name
    Environment= var.stage
    ManagedBy= "Terraform"
  }
}

# rest api gateway instead of http api

resource "aws_api_gateway_rest_api" "main" {
  name= local.api_name
  description= "REST API with extended integration timeout"
  
  endpoint_configuration {
    types =["REGIONAL"]
  }
  
  tags = {
    Name =local.api_name
  }
  
  # Don't replace existing API Gateway
  lifecycle {
    prevent_destroy= true
  }
}
# cloudWatch log group for api gateway

resource "aws_cloudwatch_log_group" "api_gateway" {
  name= "/aws/apigateway/${aws_api_gateway_rest_api.main.name}"
  retention_in_days = 30
  
  tags = {
    Name = "${var.project_name}-${var.stage}-api-gateway-logs"
  }
}

# JWT Authorizer for API Gateway
resource "aws_api_gateway_authorizer" "jwt_authorizer" {
  name= "${var.project_name}-${var.stage}-jwt-authorizer"
  rest_api_id= aws_api_gateway_rest_api.main.id
  type= "COGNITO_USER_POOLS"
  identity_source= "method.request.header.Authorization"
  
  # Configure JWT issuer and audience
  provider_arns = [var.cognito_user_pool_arn]
}

# auth resource and method

resource "aws_api_gateway_resource" "auth" {
  rest_api_id= aws_api_gateway_rest_api.main.id
  parent_id= aws_api_gateway_rest_api.main.root_resource_id
  path_part= "auth"
}

resource "aws_api_gateway_method" "auth" {
  rest_api_id= aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.auth.id
  http_method= "POST"
  authorization = "NONE"  # auth endpoint does not require authorization
}

resource "aws_api_gateway_integration" "auth" {
  rest_api_id= aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.auth.id
  http_method= aws_api_gateway_method.auth.http_method
  integration_http_method= "POST"
  type= "AWS_PROXY"
  uri= "arn:aws:apigateway:${var.aws_region}:lambda:path/2015-03-31/functions/${var.auth_handler_arn}/invocations"
  timeout_milliseconds= 30000  
}

resource "aws_api_gateway_method_response" "auth" {
  rest_api_id= aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.auth.id
  http_method = aws_api_gateway_method.auth.http_method
  status_code ="200"
  
  response_models = {
    "application/json" = "Empty"
  }
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Origin" = true
  }
}

resource "aws_api_gateway_integration_response" "auth" {
  rest_api_id= aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.auth.id
  http_method= aws_api_gateway_method.auth.http_method
  status_code= aws_api_gateway_method_response.auth.status_code
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Origin" = "'*'"
  }
  
  depends_on = [
    aws_api_gateway_integration.auth
  ]
}

# CORS support for auth endpoint

resource "aws_api_gateway_method" "auth_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.auth.id
  http_method= "OPTIONS"
  authorization ="NONE"
}

resource "aws_api_gateway_integration" "auth_options" {
  rest_api_id =aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.auth.id
  http_method= aws_api_gateway_method.auth_options.http_method
  type= "MOCK"
  
  request_templates = {
    "application/json" = "{\"statusCode\": 200}"
  }
}

resource "aws_api_gateway_method_response" "auth_options" {
  rest_api_id =aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.auth.id
  http_method = aws_api_gateway_method.auth_options.http_method
  status_code = "200"
  
  response_models = {
    "application/json" = "Empty"
  }
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Headers" = true,
    "method.response.header.Access-Control-Allow-Methods" = true,
    "method.response.header.Access-Control-Allow-Origin"  = true
  }
}

resource "aws_api_gateway_integration_response" "auth_options" {
  rest_api_id =aws_api_gateway_rest_api.main.id
  resource_id =aws_api_gateway_resource.auth.id
  http_method = aws_api_gateway_method.auth_options.http_method
  status_code = aws_api_gateway_method_response.auth_options.status_code
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Headers" = "'Content-Type,X-Amz-Date,Authorization,X-Api-Key,X-Amz-Security-Token'",
    "method.response.header.Access-Control-Allow-Methods" = "'GET,POST,OPTIONS'",
    "method.response.header.Access-Control-Allow-Origin" = "'*'"
  }
  
  depends_on = [
    aws_api_gateway_integration.auth_options
  ]
}




# query resource and method

resource "aws_api_gateway_resource" "query" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  parent_id= aws_api_gateway_rest_api.main.root_resource_id
  path_part= "query"
}

resource "aws_api_gateway_method" "query" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id  = aws_api_gateway_resource.query.id
  http_method  = "POST"
  authorization = "COGNITO_USER_POOLS"
  authorizer_id = aws_api_gateway_authorizer.jwt_authorizer.id
}

resource "aws_api_gateway_integration" "query" {
  rest_api_id             = aws_api_gateway_rest_api.main.id
  resource_id             = aws_api_gateway_resource.query.id
  http_method             = aws_api_gateway_method.query.http_method
  integration_http_method = "POST"
  type                    = "AWS_PROXY"
  uri                     = "arn:aws:apigateway:${var.aws_region}:lambda:path/2015-03-31/functions/${var.query_processor_arn}/invocations"
  timeout_milliseconds    = 150000  #2.5min
}

resource "aws_api_gateway_method_response" "query" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.query.id
  http_method = aws_api_gateway_method.query.http_method
  status_code = "200"
  
  response_models = {
    "application/json" = "Empty"
  }
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Origin" = true
  }
}

resource "aws_api_gateway_integration_response" "query" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.query.id
  http_method = aws_api_gateway_method.query.http_method
  status_code = aws_api_gateway_method_response.query.status_code
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Origin" = "'*'"
  }
  
  depends_on = [
    aws_api_gateway_integration.query
  ]
}

# upload resource and method


resource "aws_api_gateway_resource" "upload" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  parent_id   = aws_api_gateway_rest_api.main.root_resource_id
  path_part   = "upload"
}

resource "aws_api_gateway_method" "upload" {
  rest_api_id   = aws_api_gateway_rest_api.main.id
  resource_id   = aws_api_gateway_resource.upload.id
  http_method   = "POST"
  authorization = "COGNITO_USER_POOLS"
  authorizer_id = aws_api_gateway_authorizer.jwt_authorizer.id
}

resource "aws_api_gateway_integration" "upload" {
  rest_api_id             = aws_api_gateway_rest_api.main.id
  resource_id             = aws_api_gateway_resource.upload.id
  http_method             = aws_api_gateway_method.upload.http_method
  integration_http_method = "POST"
  type                    = "AWS_PROXY"
  uri                     = "arn:aws:apigateway:${var.aws_region}:lambda:path/2015-03-31/functions/${var.upload_handler_arn}/invocations"
  timeout_milliseconds    = 150000  # 2.5min
}

resource "aws_api_gateway_method_response" "upload" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.upload.id
  http_method = aws_api_gateway_method.upload.http_method
  status_code = "200"
  
  response_models = {
    "application/json" = "Empty"
  }
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Origin" = true
  }
}

resource "aws_api_gateway_integration_response" "upload" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.upload.id
  http_method = aws_api_gateway_method.upload.http_method
  status_code = aws_api_gateway_method_response.upload.status_code
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Origin" = "'*'"
  }
  
  depends_on = [
    aws_api_gateway_integration.upload
  ]
}


# CORS support
# options method for /query
 

resource "aws_api_gateway_method" "query_options" {
  rest_api_id= aws_api_gateway_rest_api.main.id
  resource_id= aws_api_gateway_resource.query.id
  http_method = "OPTIONS"
  authorization = "NONE"
}

resource "aws_api_gateway_integration" "query_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.query.id
  http_method = aws_api_gateway_method.query_options.http_method
  type        = "MOCK"
  
  request_templates = {
    "application/json" = "{\"statusCode\": 200}"
  }
}

resource "aws_api_gateway_method_response" "query_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.query.id
  http_method = aws_api_gateway_method.query_options.http_method
  status_code = "200"
  
  response_models = {
    "application/json" = "Empty"
  }
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Headers" = true,
    "method.response.header.Access-Control-Allow-Methods" = true,
    "method.response.header.Access-Control-Allow-Origin"  = true
  }
}

resource "aws_api_gateway_integration_response" "query_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.query.id
  http_method = aws_api_gateway_method.query_options.http_method
  status_code = aws_api_gateway_method_response.query_options.status_code
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Headers" = "'Content-Type,X-Amz-Date,Authorization,X-Api-Key,X-Amz-Security-Token'",
    "method.response.header.Access-Control-Allow-Methods" = "'GET,POST,OPTIONS'",
    "method.response.header.Access-Control-Allow-Origin"  = "'*'"
  }
  
  depends_on = [
    aws_api_gateway_integration.query_options
  ]
}

# options method for /upload

resource "aws_api_gateway_method" "upload_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id  = aws_api_gateway_resource.upload.id
  http_method = "OPTIONS"
  authorization = "NONE"
}

resource "aws_api_gateway_integration" "upload_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.upload.id
  http_method = aws_api_gateway_method.upload_options.http_method
  type        = "MOCK"
  
  request_templates = {
    "application/json" = "{\"statusCode\": 200}"
  }
}

resource "aws_api_gateway_method_response" "upload_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.upload.id
  http_method = aws_api_gateway_method.upload_options.http_method
  status_code = "200"
  
  response_models = {
    "application/json" = "Empty"
  }
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Headers" = true,
    "method.response.header.Access-Control-Allow-Methods" = true,
    "method.response.header.Access-Control-Allow-Origin"  = true
  }
}

resource "aws_api_gateway_integration_response" "upload_options" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  resource_id = aws_api_gateway_resource.upload.id
  http_method = aws_api_gateway_method.upload_options.http_method
  status_code = aws_api_gateway_method_response.upload_options.status_code
  
  response_parameters = {
    "method.response.header.Access-Control-Allow-Headers" = "'Content-Type,X-Amz-Date,Authorization,X-Api-Key,X-Amz-Security-Token'",
    "method.response.header.Access-Control-Allow-Methods" = "'GET,POST,OPTIONS'",
    "method.response.header.Access-Control-Allow-Origin"  = "'*'"
  }
  
  depends_on = [
    aws_api_gateway_integration.upload_options
  ]
}

# deployment and Stage

resource "aws_api_gateway_deployment" "main" {
  rest_api_id = aws_api_gateway_rest_api.main.id
  
  triggers = {
    # Original trigger configuration without health endpoint references
    redeployment = sha1(jsonencode([
      aws_api_gateway_resource.auth.id,
      aws_api_gateway_resource.query.id,
      aws_api_gateway_resource.upload.id,
      aws_api_gateway_method.auth.id,
      aws_api_gateway_method.query.id,
      aws_api_gateway_method.upload.id,
      aws_api_gateway_integration.auth.id,
      aws_api_gateway_integration.query.id,
      aws_api_gateway_integration.upload.id,
    ]))
  }
  
  lifecycle {
    create_before_destroy = true
  }
  
  depends_on = [
    aws_api_gateway_integration.auth,
    aws_api_gateway_integration.query,
    aws_api_gateway_integration.upload,
    aws_api_gateway_integration.auth_options,
    aws_api_gateway_integration.query_options,
    aws_api_gateway_integration.upload_options
  ]
}

resource "aws_api_gateway_stage" "main" {
  deployment_id = aws_api_gateway_deployment.main.id
  rest_api_id   = aws_api_gateway_rest_api.main.id
  stage_name    = var.stage
  
  access_log_settings {
    destination_arn = aws_cloudwatch_log_group.api_gateway.arn
    format = jsonencode({
      requestId= "$context.requestId"
      ip= "$context.identity.sourceIp"
      requestTime= "$context.requestTime"
      httpMethod= "$context.httpMethod"
      routeKey= "$context.resourcePath"
      status= "$context.status"
      protocol= "$context.protocol"
      responseLength= "$context.responseLength"
      integrationErrorMessage = "$context.integrationErrorMessage"
    })
  }
  
  tags = {
    Name = "${var.project_name}-${var.stage}-stage"
  }
}

#permissoins for labdas

resource "aws_lambda_permission" "api_gateway_query" {
  statement_id = "AllowAPIGatewayInvokeQuery"
  action= "lambda:InvokeFunction"
  function_name= var.query_processor_name
  principal= "apigateway.amazonaws.com"
  source_arn= "${aws_api_gateway_rest_api.main.execution_arn}/*/${aws_api_gateway_method.query.http_method}${aws_api_gateway_resource.query.path}"
}

resource "aws_lambda_permission" "api_gateway_upload" {
  statement_id  = "AllowAPIGatewayInvokeUpload"
  action= "lambda:InvokeFunction"
  function_name = var.upload_handler_name
  principal= "apigateway.amazonaws.com"
  source_arn= "${aws_api_gateway_rest_api.main.execution_arn}/*/${aws_api_gateway_method.upload.http_method}${aws_api_gateway_resource.upload.path}"
}

resource "aws_lambda_permission" "api_gateway_auth" {
  statement_id  = "AllowAPIGatewayInvokeAuth"
  action= "lambda:InvokeFunction"
  function_name = var.auth_handler_name
  principal= "apigateway.amazonaws.com"
  source_arn= "${aws_api_gateway_rest_api.main.execution_arn}/*/${aws_api_gateway_method.auth.http_method}${aws_api_gateway_resource.auth.path}"
}




